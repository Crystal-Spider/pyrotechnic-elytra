plugins {
  // Required for NeoGradle
  id "org.jetbrains.gradle.plugin.idea-ext" version "1.1.7"
}

subprojects {
  apply plugin: "java"

  java.toolchain.languageVersion = JavaLanguageVersion.of(17)
  java.withSourcesJar()
  java.withJavadocJar()

  jar {
    from(rootProject.file("LICENSE")) {
      rename { "${it}_${mod_title}" }
    }
    manifest {
      attributes([
        'Specification-Title'     : mod_title,
        'Specification-Vendor'    : mod_author,
        'Specification-Version'   : project.jar.archiveVersion,
        'Implementation-Title'    : mod_title,
        'Implementation-Vendor'   : mod_author,
        'Implementation-Version'  : project.jar.archiveVersion,
        "Implementation-URL"      : "https://github.com/${github_user}/${mod_id.replaceAll('_', '-')}/tree/${minecraft_version}",
        'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
        'Timestamp'               : System.currentTimeMillis(),
        'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
        'Built-On-Minecraft'      : minecraft_version
      ])
    }
  }

  sourcesJar {
    from(rootProject.file("LICENSE")) {
      rename { "${it}_${mod_title}" }
    }
  }

  repositories {
    mavenCentral()
    maven {
      name = "Sponge / Mixin"
      url = "https://repo.spongepowered.org/repository/maven-public/"
    }
  }

  tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.getRelease().set(17)
  }

  processResources {
    def expandProps = [
      "mod_version"                  : mod_version,
      "group"                        : project.group,
      "mod_author"                   : mod_author,
      "mod_title"                    : mod_title,
      "mod_id"                       : mod_id,
      "mod_id_kebab"                 : mod_id.replaceAll("_", "-"),
      "license"                      : license,
      "description"                  : project.description,
      "credits"                      : credits,
      "github_user"                  : github_user,
      "curseforge_id"                : curseforge_id,
      "minecraft_version"            : minecraft_version,
      "minecraft_version_range"      : minecraft_version_range,
      "fabric_version"               : fabric_version,
      "fabric_loader_version"        : fabric_loader_version,
      "forge_version"                : forge_version,
      "forge_loader_version_range"   : forge_loader_version_range,
      "neoforge_version"             : neoforge_version,
      "neoforge_loader_version_range": neoforge_loader_version_range,
    ]

    filesMatching(["pack.mcmeta", "fabric.mod.json", "META-INF/mods.toml", "*.mixins.json"]) {
      expand expandProps
    }
    inputs.properties(expandProps)
  }

  tasks.withType(GenerateModuleMetadata).configureEach {
    enabled = false
  }
}
